'DEPENDENCIES:
'1. Microsoft VBScript Regular Expressions 5.5
'2. Microsoft Scripting Runtime
'3. VBA-Library/ReadTextFile

'NOTES:
'1. Reads a user preference file generated by the VBA-Library/RecordPreferences function
'2. Data is returned in a keyed collection containing: Form ID, Filter String, Sort String, Hidden Columns, Column Order

Public Function ReadPreferences(Optional InputFormIDName As String) As Collection
    Dim Regex As New RegExp
    Dim RegexRetVal As Object
    Dim SubCol As Collection
    Dim FileStr As String
    Dim X As Integer
    Dim fso As New FileSystemObject
    Dim TxtStream As TextStream
    
    Set ReadPreferences = New Collection
    
    'If preferences file does not exist, create it
    If Dir(Application.CurrentProject.Path & "\UserPref.txt") = "" Then
        Set TxtStream = fso.CreateTextFile(Application.CurrentProject.Path & "\UserPref.txt", True)
        TxtStream.Write "NULL"
        TxtStream.Close
    End If
    
    'Read all of the preference file contents into a string
    FileStr = ReadTextFile(Application.CurrentProject.Path & "\UserPref.txt", 1)
    
    Regex.Global = True
    Regex.MultiLine = True
    Regex.IgnoreCase = True
    
    Regex.Pattern = "<FORM>[\s]*<ID>([\S ]*)</ID>[\s]*<FILTER>([\S ]*)</FILTER>[\s]*<SORT>([\S ]*)</SORT>[\s]*<HIDE>([\S ]*)</HIDE>[\s]*<ORDER>([\S ]*)</ORDER>[\s]*</FORM>"
    Set RegexRetVal = Regex.Execute(FileStr)
    
    'Loop through Regex parsed FileStr and record each form entry as a subcollection in the master collection
    X = 0
    Do Until X >= RegexRetVal.Count
        Set SubCol = New Collection

        SubCol.Add RegexRetVal(X).SubMatches(0), "ID"
        SubCol.Add RegexRetVal(X).SubMatches(1), "FILTER"
        SubCol.Add RegexRetVal(X).SubMatches(2), "SORT"
        SubCol.Add RegexRetVal(X).SubMatches(3), "HIDE"
        SubCol.Add RegexRetVal(X).SubMatches(4), "ORDER"
        ReadPreferences.Add SubCol
        
        X = X + 1
    Loop
    
End Function
